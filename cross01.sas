

%macro chk_sdtm_adam(sdtm=, adam=);

libname sdtm "&sdtm";
libname adam "&adam";

*** get the metadata for SDTM *** ;

%list_files(path=&sdtm);

data f1; set f1;
name=scan(names,1, '.');
if upcase(substr(name,1,4))='SUPP' then delete;
run;

data _null_; 
set f1 end=eof;
i+1;
call symput(compress("dataset"||i), trim(left(name)));
if eof then call symput("tota", trim(left(_n_)));
run;

%do r=1 %to &tota;
%if %sysfunc(exist(sdtm.supp&&dataset&r)) %then %do; 
%supp(inlib=sdtm, insupp=supp&&dataset&r);
%end;
%else %do;
data &&dataset&r; set sdtm.&&dataset&r;
%end;
%end;

%do s=1 %to &tota;
proc contents data=&&dataset&s noprint out=out&s;
data combine_sdtm(keep=memname name label type length); set %if &s=1 %then out1; %else combine_sdtm out&s;; 
%end;

*** get the metadata for ADaM *** ;

%list_files(path=&adam);

data f1; set f1;
name=scan(names,1, '.');
run;

data _null_; 
set f1 end=eof;
i+1;
call symput(compress("dataset"||i), trim(left(name)));
if eof then call symput("totb", trim(left(_n_)));
run;

%do s=1 %to &totb;
proc contents data=adam.&&dataset&s noprint out=out&s;
data combine_adam(keep=memname name label type length); set %if &s=1 %then out1; %else combine_adam out&s;; 
%end;

*** compare the metadata from SDTM and ADaM ***; 

data combine_sdtm; set combine_sdtm;
rename memname=memname_sdtm label=label_sdtm type=type_sdtm length=length_sdtm;

data combine_adam; set combine_adam;
rename memname=memname_adam label=label_adam type=type_adam length=length_adam;

proc sort data=combine_sdtm nodupkey dupout=chk; by name;
proc sort data=combine_adam; by name;

data final;
merge combine_sdtm(in=a) combine_adam(in=b);
by name;
if a and b;
run;

data final; retain name; set final;

ods pdf file="C:\review\cross01.pdf"; 

proc print data=final(keep=name memname: label:);
where label_sdtm ne label_adam;
title1 "Label is different in SDTM and ADaM";
title2 "The label in SDTM is from SDTM domain or SUPPQUAL";
footnote "Generated by chk_sdtm_adam.sas, &sysdate &systime SAS &sysver in &sysscpl";

proc print data=final(keep=name memname: type:);
where type_sdtm ne type_adam;
title1 "Type is different in SDTM and ADaM";
title2 "The Type in SDTM is from SDTM domain or SUPPQUAL";
footnote "Generated by chk_sdtm_adam.sas, &sysdate &systime SAS &sysver in &sysscpl";
run;

ods pdf close; 

proc datasets kill lib=work memtype=data nodetails nolist;
run;
%mend;


** example ; 
options sasautos=("C:\review\checklist");
%chk_sdtm_adam(sdtm=C:\review\sdtm, adam=C:\review\adam\datasets);


